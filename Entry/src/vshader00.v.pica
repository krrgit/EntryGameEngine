; Example PICA200 vertex shader

; Uniforms
.fvec u_Projection[4], u_ModelView[4]

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.1)
.constf myconst2(0.3, 0.0, 0.0, 0.0)
.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones

; Outputs
.out outpos position
.out outclr color

; Inputs (defined as aliases for convenience)
.alias inpos v0
.alias inclr v1

.proc main
	; Force the w component of inpos to be 1.0
	mov r0.xyz, inpos
	mov r0.w,   ones

	; r1 = modelView * inpos
	dp4 r1.x, u_ModelView[0], r0
	dp4 r1.y, u_ModelView[1], r0
	dp4 r1.z, u_ModelView[2], r0
	dp4 r1.w, u_ModelView[3], r0

	; outpos = projection * r1
	dp4 outpos.x, u_Projection[0], r1
	dp4 outpos.y, u_Projection[1], r1
	dp4 outpos.z, u_Projection[2], r1
	dp4 outpos.w, u_Projection[3], r1
	
	; outclr = inclr
	mov outclr, inclr

	; We're finished
	end
.end
